import com.android.build.gradle.internal.tasks.DefaultAndroidTask

apply plugin: 'com.android.application'
apply plugin: 'android-chinese-string'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.hippo.ehviewer"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 80
        versionName "1.0.27"
        vectorDrawables.useSupportLibrary = true
        resConfigs "zh", "zh-rCN", "zh-rHK", "zh-rTW",
                "es", "ja", "ko"
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "EhViewer"
            buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"com.hippo.ehviewer.fileprovider"'
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "EhViewerDebug"
            buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"com.hippo.ehviewer.debug.fileprovider"'
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java-gen'
        }
    }
}

task copyNotice(type: Copy) {
    from '../NOTICE.html'
    into './src/main/assets'
    finalizedBy ":daogenerator:executeDaoGenerator"
}

tasks.withType(DefaultAndroidTask) {
    task -> task.dependsOn copyNotice
}

clean {
    delete file('src/main/assets/NOTICE.html').absolutePath
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$supportLibrary"
    implementation "com.android.support:design:$supportLibrary"
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation 'com.github.seven332:android-recaptcha:2bbb3459a8'
    implementation 'com.github.seven332:animator:0.1.0'
    implementation 'com.github.seven332:beerbelly:0.1.4'
    implementation 'com.github.seven332:conaco:0.1.5-eh'
    implementation 'com.github.seven332:drawerlayout:0.2.1'
    implementation 'com.github.seven332:easyrecyclerview:0.1.1'
    implementation 'com.github.seven332:glgallery:0.1.2'
    implementation 'com.github.seven332:glview:0.1.0'
    implementation 'com.github.seven332:glview-image:0.1.0'
    implementation 'com.github.seven332:hotspot:0.1.0'
    implementation 'com.github.seven332:image:0.1.12'
    implementation 'com.github.seven332:okhttp:3.5.0'
    implementation 'com.github.seven332:refreshlayout:0.1.0'
    implementation 'com.github.seven332:ripple:0.1.2'
    implementation 'com.github.seven332:streampipe:0.1.0'
    implementation 'com.github.seven332:tuxiang:0.1.2'
    implementation 'com.github.seven332:unifile:0.2.0'
    implementation 'com.github.seven332:yorozuya:0.1.2'
    implementation 'com.github.seven332:yorozuya-thread:0.1.1'
    implementation 'com.github.seven332:yorozuya-collect:0.1.4'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.9.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    implementation 'org.greenrobot:greendao:2.2.1'
    implementation 'org.jsoup:jsoup:1.11.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.4'
    testImplementation 'org.jooq:joor:0.9.6'
}

configurations.all {
    resolutionStrategy {
        force 'com.github.seven332:glgallery:0f276c0'
        force 'com.github.seven332:glview-image:4c744db'
        force 'com.github.seven332:image:bab9b11'

        exclude group: 'com.github.seven332', module: 'okhttp'
    }
}
